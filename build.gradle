buildscript {
  ext {
    app_name = "app"
    class_name = "package"

    kotlin_version = '1.2.70'
    vertx_version = '3.5.3'
    shadow_version = '2.0.4'
  }

  repositories {
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
  }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
  jcenter()
}

dependencies {
  compile "io.vertx:vertx-core:$vertx_version"
  compile "io.vertx:vertx-web:$vertx_version"

  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
  compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
}

//sourceCompatibility = '1.8'

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'io.vertx.example.MainVerticle'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

// Redeploy watcher.
run {
  args = ['run', mainVerticleName,
          "--launcher-class=$mainClassName",
          "--redeploy=src/**/*.*",
          "--on-redeploy=./gradlew classes"
  ]
}

// Naming and packaging settings for the "shadow jar".
shadowJar {
  baseName = "$app_name"
  classifier = "$class_name"

  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

// Heroku relies on the 'stage' task to deploy.
task stage {
  dependsOn shadowJar
}